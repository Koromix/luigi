open_window("Mighty Screensaver")

COLORS = ["red", "yellow", "green"]

size = get_window_size()
pos = { x = size.width / 2, y = size.height / 2 }
speed = { x = 1, y = 1 }
color = 0

func cycle_color()
    color = color + 1
    if not color < length(COLORS) then color = 0
end

while run_window("black")
    size = get_window_size()

    text = size.width + "x" + size.height
    box = draw_text(pos.x, pos.y, { align = 5, color = COLORS[color] }, text)

    if speed.x = 1 and pos.x >= size.width - box.width / 2
        speed.x = -1
        cycle_color()
    end
    if speed.x = -1 and pos.x <= box.width / 2
        speed.x = 1
        cycle_color()
    end
    if speed.y = 1 and pos.y >= size.height - box.height / 2
        speed.y = -1
        cycle_color()
    end
    if speed.y = -1 and pos.y <= box.height / 2
        speed.y = 1
        cycle_color()
    end

    pos.x = pos.x + speed.x * 2
    pos.y = pos.y + speed.y * 2
end
